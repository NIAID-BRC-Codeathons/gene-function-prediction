#! /usr/bin/perl
#
#  freqs_2_dists [options] < freqs_file  > distances
#
#  Distance is Manhatten metric within amino acid, and Euclidian
#  between amino acids
#

use strict;
use gjocodonlib;

sub usage
{
    print STDERR <<'End_of_usage';

freqs_2_dists [options] < freqs_file  > distances

Options:

    -l label_file  # Provide a translation file to orginal labels
    -t             # Tab delimited (not PHYLIP)

End_of_usage

exit;
}

my $lbl_file;
my $tab = 0;

while ( @ARGV && $ARGV[0] =~ s/^-// )
{
    local $_ = shift;
    if ( s/^l// ) { $lbl_file = $_ || shift; next }

    if ( s/t//  ) { $tab = 1 }
    if ( m/./ )
    {
        print STDERR "Bad flag: '$_'\n";
        usage();
    }
}

my $i = '00000';
my @freqs;
my %labels;

while ( <> )
{
    chomp;
    s/\s+$//;
    my @f = split /\t/;
    shift @f if $f[0] !~ /,/;  # Remove score if present
    next if $f[0] !~ /,/;      # No frequencies
    $i++;
    my $lbl1 = $f[1] || "otu$i";
    my $lbl2 = $tab ? $lbl1 : valid_label( $lbl1, \%labels, "otu$i", $lbl_file );
    $labels{ $lbl2 } = $lbl1;
    push @freqs, [ split_freq( $f[0] ), $lbl2 ];
}

my $delim = $tab ? "\t" : " ";
print $i+0 . "\n" if ! $tab;
for ( my $f1 = 0; $f1 < @freqs; $f1++ )
{
    my $freqs1 = $freqs[$f1]->[0];
    my @line = ( $tab ? $freqs[$f1]->[1] : sprintf "%-10s  ", $freqs[$f1]->[1] );
    for ( my $f2 = 0; $f2 < @freqs; $f2++ )
    {
        #
        #  codon_freq_distance_2 is Manhatten metric within amino acid, and
        #  Euclidian between amino acids.
        #
        push @line, sprintf( "%.4f", gjocodonlib::codon_freq_distance_2( $freqs1, $freqs[$f2]->[0] ) );
    }
    print join( $delim, @line), "\n";
}

if ( $lbl_file && open( LBL, ">$lbl_file" ) )
{
    foreach ( map { $_->[1] } @freqs ) { print LBL "$_\t$labels{$_}\n" }
}
else
{
    my $okay = 1;
    foreach ( map { $_->[1] } @freqs )
    {
        next if $_ eq $labels{$_};
        print STDERR "WARNING: '$labels{$_}' is being relabeled to '$_'\n";
        $okay = 0;
    }
    print STDERR "\nUse '-l label_file' option to record changed names.\n\n" if ! $okay;
}

exit;


sub valid_label
{
    my ( $lbl1, $labels, $dflt, $save ) = @_;

    my $lbl2 = $save                 ? $dflt
             : length( $lbl1 ) <= 10 ? $lbl1
             :                         substr( $lbl1, 0, 10 );
    my $j = 1;
    my $tmp = $lbl2;
    while ( $labels->{ $lbl2 } )
    {
        my $maxl = 9 - length( $j );
        $tmp = substr( $tmp, 0, $maxl ) if length( $tmp ) > $maxl;
        $lbl2 = "$tmp.$j";
        $j++;
    }
    return $lbl2;
}


#  Split frequences with amino acids separated by vertical bars, and
#  codons within the amino acid separated by commas:
#
#  fc1aa1,fc2aa1,fc3aa1,fc4aa1|fc1aa2,fc2aa2,...|fc1aa3,fc2aa3,...

sub split_freq { [ map { [ map { $_ + 0 } split /,/ ] } split /\|/, shift ] }

